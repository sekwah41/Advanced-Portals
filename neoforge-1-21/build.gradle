import org.apache.http.HttpEntity
import org.apache.http.client.methods.CloseableHttpResponse
import org.apache.http.client.methods.HttpPost
import org.apache.http.entity.ContentType
import org.apache.http.entity.mime.MultipartEntityBuilder
import org.apache.http.impl.client.CloseableHttpClient
import org.apache.http.impl.client.HttpClients

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = "https://repo.spongepowered.org/maven" }
        //maven { url = 'https://sizableshrimp.me/maven' }
        maven { url = "https://plugins.gradle.org/m2/" }
        maven { url = "https://maven.parchmentmc.org" }
        mavenCentral()
    }
    dependencies {
        classpath(group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true)
        classpath "org.apache.httpcomponents:httpmime:4.5.14"
        classpath "org.parchmentmc:librarian:1.+"
    }
}
plugins {
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'earth.terrarium.cloche' version '0.8.20'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

apply plugin: "java"
apply plugin: 'eclipse'
apply plugin: 'maven-publish'


apply from: rootProject.file('env-variables.gradle')
apply from: rootProject.file('changelog-util.gradle')

apply from: rootProject.file('shadowJar.gradle')

println "Branch ${branch}${shaRef} isRelease: '${isRelease}'"

archivesBaseName = "Advanced-Portals-Forge-1-21"

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

var mc_version = "1.21.4"
var neo_version = "21.4.123"

repositories {
    mavenLocal()
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    cloche {
        librariesMinecraft()
    }

    mavenCentral()

    cloche {
        main()

        mavenFabric()
        mavenNeoforged()
    }
}

base {
    archivesName = project.modid
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

assemble.dependsOn shadowJar

shadowJar {
    configurations = [project.configurations.shade]
    mergeServiceFiles()
}

// https://github.com/terrarium-earth/cloche
cloche {
    minecraftVersion = mc_version

    metadata {
        modId = project.modid
        name = project.name
        license = project.mod_license
        description = project.mod_description
        author = project.author
    }

    singleTarget {
        neoforge {
            loaderVersion = neo_version

            runs {
                server
                client
                data
            }
        }
    }
}

dependencies {
    implementation project(":core")
    implementation(group: 'com.google.inject', name: 'guice', version: '5.0.1')
    neoforgeInclude(group: 'com.google.inject', name: 'guice', version: '5.0.1')
}

def getReleaseChangelog() {
    def changelogFile = rootProject.file('CHANGELOG.md')
    def changelog = "## [${changelogFile.text.split('\n## \\[')[1]}\n\n"// ${project.github}/blob/${branch}/CHANGELOG.md
    return changelog;
}

/**
 * For pre-releases and testers to be able to try the latest commits if they want.
 * If the builds start exceeding 8MB then we may want to upload to s3 instead and periodically clear.
 * TODO possibly add a task that announces when builds are made?
 * Though add a note that it may take a while for Curse to approve the files.
 */
tasks.register('discordupload') {
    dependsOn(jar)
    doLast {
        String discordWebhook = System.getenv("DISCORD_WEBHOOK")

        if (discordWebhook != null) {

            CloseableHttpClient httpClient = HttpClients.createDefault()
            HttpPost uploadFile = new HttpPost(discordWebhook)

            MultipartEntityBuilder builder = MultipartEntityBuilder.create()
            if (!isRelease) {
                builder.addTextBody("content", "New snapshot or testing build")
            } else {
                builder.addTextBody("content", "New release build\n" +
                        "```markdown\n" +
                        "${getReleaseChangelog()}\n" +
                        "```")
            }

            var name = jar.archiveBaseName.get() + "-" + jar.archiveVersion.get() + ".jar"

            builder.addBinaryBody("file", file(jar.archiveFile).newInputStream(), ContentType.APPLICATION_OCTET_STREAM, name)

            HttpEntity multipart = builder.build()

            uploadFile.setEntity(multipart)
            CloseableHttpResponse response = httpClient.execute(uploadFile)
            response.getEntity()

            println("Posted build")

        } else {
            println("Discord webhook unspecified")
        }
    }
}

tasks.curseforge.enabled = System.getenv("CURSE_API") != null

//curseforge {
//    logger.info("Curse api: " + System.getenv("CURSE_API"))
//    if (System.getenv("CURSE_API") != null) {
//        apiKey = System.getenv("CURSE_API")
//    }
//    project {
//        id = project.curse_project_id
//        // TODO add code to reference this but also cut the latest change logs in for the files
//        changelog = getReleaseChangelog()
//        changelogType = 'markdown'
//        releaseType = 'release'
//        addGameVersion '1.20'
//        addGameVersion '1.20.1'
//
//        mainArtifact(jar){
//
//        }
//        addArtifact srcJar
//        addArtifact deobfJar
//    }
//}

// https://github.com/modrinth/minotaur
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = project.modrinth_slug
    versionType = "release"
    uploadFile = jar
    gameVersions = ["1.20", "1.20.1"]
    changelog = getReleaseChangelog()
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

